cmake_minimum_required(VERSION 3.5...4.0)

project(tetter VERSION 1.0.0 LANGUAGES CXX)

set(MAIN_PROJECT OFF)
if (CMAKE_CURRENT_SOURCE_DIR STREQUAL CMAKE_SOURCE_DIR)
    set(MAIN_PROJECT ON)
endif()

option(TETTER_INSTALL "Install CMake targets during install step." ${MAIN_PROJECT})

include(GNUInstallDirs)

set(TETTER_CONFIG_INSTALL_DIR "${CMAKE_INSTALL_DATADIR}/cmake/${PROJECT_NAME}" CACHE INTERNAL "")
set(TETTER_INCLUDE_INSTALL_DIR "${CMAKE_INSTALL_INCLUDEDIR}")
set(TETTER_INCLUDE_BUILD_DIR "${PROJECT_SOURCE_DIR}/include/")
set(TETTER_TARGETS_EXPORT_NAME "${PROJECT_NAME}Targets")
set(TETTER_CMAKE_VERSION_CONFIG_FILE "${PROJECT_NAME}ConfigVersion.cmake")
set(TETTER_CMAKE_PROJECT_CONFIG_FILE "${PROJECT_NAME}Config.cmake")

add_library(${PROJECT_NAME} INTERFACE)
add_library(${PROJECT_NAME}::${PROJECT_NAME} ALIAS ${PROJECT_NAME})
target_include_directories(${PROJECT_NAME} INTERFACE
    $<BUILD_INTERFACE:${TETTER_INCLUDE_BUILD_DIR}>
    $<INSTALL_INTERFACE:${TETTER_INCLUDE_INSTALL_DIR}>
)

if(TETTER_INSTALL)
    install(
        DIRECTORY ${TETTER_INCLUDE_BUILD_DIR}
        DESTINATION ${TETTER_INCLUDE_INSTALL_DIR}
    )

    install(
        TARGETS ${PROJECT_NAME}
        EXPORT ${TETTER_TARGETS_EXPORT_NAME}
        INCLUDES DESTINATION ${TETTER_INCLUDE_INSTALL_DIR}
    )

    install(
        EXPORT ${TETTER_TARGETS_EXPORT_NAME}
        FILE ${TETTER_CMAKE_PROJECT_CONFIG_FILE}
        NAMESPACE ${PROJECT_NAME}::
        DESTINATION ${TETTER_CONFIG_INSTALL_DIR}
    )

    include(CMakePackageConfigHelpers)
    write_basic_package_version_file(
        "${CMAKE_CURRENT_BINARY_DIR}/${TETTER_CMAKE_VERSION_CONFIG_FILE}"
        VERSION ${PROJECT_VERSION}
        COMPATIBILITY SameMajorVersion
    )
    install(
        FILES "${CMAKE_CURRENT_BINARY_DIR}/${TETTER_CMAKE_VERSION_CONFIG_FILE}"
        DESTINATION ${TETTER_CONFIG_INSTALL_DIR}
    )
endif()
